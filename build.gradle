import groovy.xml.MarkupBuilder

buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.flywaydb:flyway-core:5.0.6'
        classpath 'org.postgresql:postgresql:9.4.1212.jre7'
    }
}


plugins {
    // add flyway gradle tasks
    id "org.flywaydb.flyway" version "5.0.6"

    // add jooq code generator tasks
    id 'nu.studer.jooq' version '2.0.9'

    // configure annotation processor plugin (we use this instead of gradle's built in apt configuration
    // because this one does some extra convenience items, such as allowing us to configure where
    // generated source code goes and wiring up IntelliJ to know about it when you run ./gradlew idea
    id 'net.ltgt.apt' version '0.13'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'

group = 'com.p202'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


ext {
    springCloudVersion = 'Edgware.SR1'
    jooqVersion = '3.10.1'
}


dependencies {

    // database driver to use for jooq code generator
    jooqRuntime 'org.postgresql:postgresql:9.4.1212.jre7'

    apt 'org.mapstruct:mapstruct-processor:1.2.0.Final'


    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-actuator-docs'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-web'

    compile 'org.jooq:jooq'
    //noinspection GrUnresolvedAccess
    compile "org.flywaydb:flyway-core:$flyway_version"

    implementation 'org.mapstruct:mapstruct:1.2.0.Final'

    compile 'org.modelmapper.extensions:modelmapper-jooq:1.1.2'
    compile 'org.modelmapper:modelmapper:1.1.0'

    runtime 'org.springframework.boot:spring-boot-devtools'
    runtime 'org.postgresql:postgresql:9.4.1212.jre7'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

/**
 * Configure flyway
 */
flyway {
    url = "jdbc:postgresql://$db_host:$db_port/jsx"
}

/**
 * Configure the jooq code generator plugin
 */
jooq {
    version = jooqVersion
    edition = 'OSS'

    jsx(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = "jdbc:postgresql://$db_host:$db_port/jsx"
            user = db_admin_user
            password = db_admin_password
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                schemata {
                    schema {
                        inputSchema = 'core'
                    }
                    schema {
                        inputSchema = 'iam'
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'com.p202.jsx.jooq'
                directory = 'src/generated/java'
            }
        }
    }
}

generateJsxJooqSchemaSource.dependsOn flywayMigrate

compileJava {
    generatedSourcesDestinationDir = 'src/generated/java'
}




